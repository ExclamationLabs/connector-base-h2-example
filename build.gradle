buildscript {
    ext {}
    dependencies {
        classpath(group: 'com.exclamationlabs.connid', name: 'connector-base-config-plugin', version: "${config_plugin_version}-+")
    }
    repositories {
        maven {
            url "https://us-east1-maven.pkg.dev/provision-general-operations/provision-public"
        }
    }
}

plugins {
    id 'java'
    id 'com.google.cloud.artifactregistry.gradle-plugin' version '2.2.0'
}

repositories {
    mavenCentral()
    maven {
        url "https://nexus.evolveum.com/nexus/content/repositories/releases/"
    }
    maven {
        url "https://us-east1-maven.pkg.dev/provision-general-operations/provision-public"
    }
}

apply plugin: 'com.exclamationlabs.connid.base.config.plugin'

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

def versionNumber = "${project_version}"

dependencies {

    // Required for MidPoint runtime
    runtimeOnly 'org.codehaus.groovy:groovy:2.5.2'
    runtimeOnly 'com.evolveum.polygon:connector-common:1.5.0.0'

    // HTTP/RESTful support
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'org.apache.httpcomponents:httpclient:4.5.10'

    // H2 database support - for this example project
    implementation "com.h2database:h2:1.4.200"

    implementation "com.exclamationlabs.connid:connector-base:${base_connector_version}-+"

    testImplementation "com.exclamationlabs.connid:connector-base-test-support:${test_connector_version}-+"

    testImplementation 'net.tirasa.connid:connector-framework-internal:1.5.0.0'

    // Example: pulling down source code and javadocs
    //implementation "com.exclamationlabs.connid:connector-base:${base_connector_version}-+:javadoc"
    //implementation "com.exclamationlabs.connid:connector-base:${base_connector_version}-+:source"

}

sourceSets {
    main {
        java {
            srcDirs = ['src/main/java', 'generatedConfig']
        }
    }
}


task copyDependencyJars(type: Copy) {
    from configurations.runtimeClasspath.collect {
        if(it.isFile() && it.getCanonicalFile().getCanonicalPath().endsWith('.jar') &&
                (!it.getCanonicalFile().getCanonicalPath().contains("connector-framework"))) {
            it
        }
    }
    into file("custom/lib")
}

task baseClean(type: Delete) {
    delete 'custom'
    delete 'generatedConfig'
}

jar {
    duplicatesStrategy DuplicatesStrategy.EXCLUDE
    archiveName("${project.name}-${versionNumber}-connector.jar")
    from 'custom', 'build/classes/java/main', 'src/main/resources'

    manifest {
        attributes("ConnectorBundle-FrameworkVersion": "1.4.3.11",
                "ConnectorBundle-Version": versionNumber,
                "ConnectorBundle-Name": project.name)
    }
}

compileJava.dependsOn('generateConfiguration')

clean.dependsOn('baseClean')
jar.dependsOn('copyDependencyJars')